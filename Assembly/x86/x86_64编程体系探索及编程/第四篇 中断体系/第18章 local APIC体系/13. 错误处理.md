[TOC]

当在local APIC处理中断时检测到错误的发生，local APIC的ESR（error status register，错误状态寄存器）将记录发生的错误条件。

![config](./images/65.png)

ESR共可记录8个发生的错误，图中的Receive Accept Error（bit 3）、Send Accept Error（bit 2）、Receive Checksum Error（bit 1），以及Send Checksum Error（bit 0）这些状态位仅使用于P6和Pentium处理器上，这里我们忽略这些状态位。

① Redirectable IPI（bit 4）：当local APIC发送一个lowest-priority delivery模式的IPI消息时，如果处理器的local APIC是不支持该类型的delivery模式，则将发生错误记录在ESR寄存器的bit 4位上。

② Send Illegal Vector（bit 5）：当local APIC发送一个IPI消息时，使用了无效的vector值（如前面的18.9.2节所描述的无效vector），ESR的bit 5将被置位记录这个错误。

③ Recevie Illegal Vector（bit 6）：当local APIC接收到一个IPI消息，或者从LVT产生的本地中断源里，检测到使用了无效的vector值时，ESR的bit 6被置位记录这个错误。值得注意的是，这个错误发生后IPI和本地中断请求没有被提交到处理器，因此IRR（interrupt request register）也没有置位指示有中断请求发生。

④ Illegal Register Address（bit 7）：当软件尝试访问保留的local APIC寄存器地址（即不支持的local APIC寄存器）时，产生错误记录在ESR的bit 7位。

Intel明确提到，ESR是可读/写的寄存器，在读ESR值前，应先写ESR，如下面的read\_esr（）函数所示。

代码清单18-32（lib\apic.asm）：
```x86asm
；-----------------------------------
； read_esr（）：读 ESR
；-----------------------------------
read_esr：
       mov DWORD [APIC_BASE + ESR]，0  ； 写入 ESR （任何值）
       mov eax，[APIC_BASE + ESR]
       ret
```

往ESR写入一个值，将使得local APIC更新ESR的状态信息，这个状态是ESR最新的状态。这个写入的值不影响ESR的读出值。

```x86asm
mov DWORD [APIC_BASE + 0]，0   ； 访问一个保留的 local APIC 地址空间
call read_esr       ； 读 ESR 值
```

在上面的代码里，访问[APIC_BASE+0]这个local APIC地址空间将产生Illegal Register Address错误，ESR的bit 7将被置位。

![config](./images/66.png)

ESR读出来的值是0x00000080。

# 1 清ESR

向ESR写入一个任意值，将清ESR的所有错误状态（ESR清为0）。这里其实是进行back-to-back写入操作，如果需要获得写入后的ESR值，实际上等于写2次ESR（第2次写入是为了读取ESR）。这与读ESR前写入值的情形不一样（这种情况是写入1次，读取1次）。

下面的代码将清ESR。

```x86asm
mov DWORD [APIC_BASE + ESR]，1  ； 写入 ESR（清ESR）
mov DWORD [APIC_BASE + ESR]，1  ； 再次写入 ESR（为了读取ESR）
mov eax，[APIC_BASE + ESR]    ； 读 ESR（写入ESR后的值）
```

上面代码的后两条指令可以使用前面的read_esr（）函数来代替实现读取ESR。第2次的写入操作是为了对ESR进行读取。

# 2 LVT error寄存器

软件设置LVT error寄存器提供相应的local APIC错误处理机制。当LVT timer寄存器被设置为提供non-masked的error中断处理器时，local APIC检测错误将引起error中断的产生，并设置ESR寄存器的相关位。

而在masked（屏蔽）的error中断里，APIC error中断处理程序不会得到调用，local APIC在ESR里置相应的错误状态。

![config](./images/67.png)

在上图的LVT error寄存器里，软件需提供一个error中断处理程序的vector值，而bit 16位是mask位，为1时设置masked的error中断。LVT error寄存器设置的error中断只能使用Fixed delivery模式及edge触发模式。

```x86asm
；  开启APIC
       call enable_xapic
；  设置 APIC error handler
       mov esi，APIC_ERROR_VECTOR
       mov edi，apic_error_handler
       call set_interrupt_handler
； 设置 LVT error 寄存器，使用non-masked错误处理
       mov DWORD [APIC_BASE + LVT_ERROR]，APIC_ERROR_VECTOR
；； 产生错误
       mov DWORD [APIC_BASE + 0]，0   ； 产生Illegal Register Address错误
       jmp $
```

在上面的代码里设置了LVT error寄存器，使用了non-masked（非屏蔽）的error中断处理。接下来对[APIC_BASE+0]寄存器地址进行访问将会产生无效的寄存器地址错误。

# 3 发送IPI消息的错误

在处理器发送IPI消息使用无效的vector时，将产生两个APIC error。

① 发送方产生send illegal vector错误。

② 接收方产生receive illegal vector错误。

当APIC error中断是non\-masked时，将在两个处理器间产生APIC error中断处理程序的调用。

>实验18-12：测试IPI消息中的APIC error中断

在这个实验里，完整的源代码在topic18\ex18-12\protected.asm文件里。我们将使用下面的代码测试发送IPI消息时产生的error中断。

```x86asm
mov DWORD [APIC_BASE + ICR1]，02000000h   ； 给 processor 1 发送 IPI
mov DWORD [APIC_BASE + ICR0]，LOGICAL_ID | 0 ； no shorthand，logical mode，fixed
```

代码中使用了logical目标模式，给processor #1发送IPI消息，这个IPI消息提供的vector为0，这是一个无效的vector。下面是在Core i5处理器上的运行结果。

![config](./images/68.png)

在这个结果里，我们看到APIC error中断handler被调用了2次：BSP发送了一条IPI消息到processor 01（使用了logical目标模式），产生了error中断（APIC ID为00000000h）。处理器01（它的APIC ID为00000001h）也产生了error中断。

注意：这两个产生error中断的原因是不同的，一个是send illegal vector错误，一个是recevie illegal vector错误（它们的ESR值不同）。